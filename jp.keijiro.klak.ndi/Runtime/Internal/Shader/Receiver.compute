#pragma kernel DecodeUYVY
#pragma kernel DecodeUYVA

StructuredBuffer<uint> Source;
RWTexture2D<float4> Destination;

// sRGB to linear conversion
float3 SRGBToLinear(float3 c)
{
    float3 lo = c / 12.92;
    float3 hi = pow((abs(c) + 0.055) / 1.055, 2.4);
    return c <= 0.04045 ? lo : hi;
}

// YUV to RGB conversion (based on Rec.709 constants)
float3 YUV2RGB(float3 yuv)
{
    const float K_B = 0.0722;
    const float K_R = 0.2126;
    const float K_G = 1 - K_B - K_R;

    float y = yuv.x;
    float u = yuv.y;
    float v = yuv.z;

    float r = y + v * (1 - K_R);
    float b = y + u * (1 - K_B);
    float g = (y - K_R * r - K_B * b) / K_G;

    // As far as I know, only Metal supports sRGB write to a UAV, so do
    // sRGB-to-linear conversion only on Metal.
    #ifdef SHADER_API_METAL
    return SRGBToLinear(float3(r, g, b));
    #else
    return float3(r, g, b);
    #endif
}

// Packed UYVY conversion
float4 UnpackUYVY(uint v)
{
    float4 f = uint4(v, v >> 8, v >> 16, v >> 24) & 0xff;
    f.yw = (f.yw -  16) / 219; //  Y components (limited range)
    f.xz = (f.xz - 128) / 112; // UV components (limited range)
    return f;
}

// Packed alpha conversion
float4 UnpackAAAA(uint v)
{
    return (uint4(v, v >> 8, v >> 16, v >> 24) & 0xff) / 255.0;
}

[numthreads(8, 8, 1)]
void DecodeUYVY(uint2 id : SV_DispatchThreadID)
{
    uint w, h;
    Destination.GetDimensions(w, h);

    uint id_y = h - 1 - id.y;

    uint sp = id_y * w / 2 + id.x;
    float4 uyvy = UnpackUYVY(Source[sp]);

    uint2 dp = id * uint2(2, 1);
    Destination[dp + uint2(0, 0)] = float4(YUV2RGB(uyvy.yxz), 1);
    Destination[dp + uint2(1, 0)] = float4(YUV2RGB(uyvy.wxz), 1);
}

[numthreads(4, 8, 1)]
void DecodeUYVA(uint2 id : SV_DispatchThreadID)
{
    uint w, h;
    Destination.GetDimensions(w, h);

    uint id_y = h - 1 - id.y;

    uint sp1 = id_y * w / 2 + id.x * 2;
    float4 uyvy01 = UnpackUYVY(Source[sp1 + 0]);
    float4 uyvy23 = UnpackUYVY(Source[sp1 + 1]);

    uint sp2 = id_y * w / 4 + id.x + w * h / 2;
    float4 a0123 = UnpackAAAA(Source[sp2]);

    uint2 dp = id * uint2(4, 1);
    Destination[dp + uint2(0, 0)] = float4(YUV2RGB(uyvy01.yxz), a0123.x);
    Destination[dp + uint2(1, 0)] = float4(YUV2RGB(uyvy01.wxz), a0123.y);
    Destination[dp + uint2(2, 0)] = float4(YUV2RGB(uyvy23.yxz), a0123.z);
    Destination[dp + uint2(3, 0)] = float4(YUV2RGB(uyvy23.wxz), a0123.w);
}
